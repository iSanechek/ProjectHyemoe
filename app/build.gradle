import java.text.DateFormat
import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

kotlin {
    experimental {
        coroutines "enable"
    }
}

androidExtensions {
    experimental = true
}

static def getDateTime() {
    DateFormat df = new SimpleDateFormat("dd MMMMM yyyy");
    return df.format(new Date()) + " Ð³.";
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    def fileNaming = "apk/ProjectHyemoe"
    def versionNumber = 1
    def versionMajor = "2"
    def versionMinor = "0"
    def versionPatch = "0"

    def versionBuild = -1
    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
        versionProps['VERSION_BUILD'] = versionBuild.toString()
        versionProps['DATE_BUILD'] = getDateTime()
        versionProps.store(versionPropsFile.newWriter(), null)
    }

    def appId = "my.wallpapers.averdsoft"
    defaultConfig {
        applicationId appId
        minSdkVersion 21
        targetSdkVersion 27
        versionCode versionNumber
        versionName "${versionMajor}.${versionMinor}.${versionPatch} (${versionBuild})"
        vectorDrawables.useSupportLibrary = true
//        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

//    flavorDimensions "wtf"
//    productFlavors {
//        stable {
//            dimension "wtf"
//        }
//        beta {
//            applicationId "${appId}.beta"
//            versionCode versionNumber
//            versionName "${versionMajor}.${versionMinor}.${versionPatch} (${versionBuild}) beta"
//            dimension "wtf"
//        }
//    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${fileNaming}-${versionMajor}.${versionMinor}.${versionPatch}.apk"
        }
    }
    sourceSets {
        main {
            java {
                srcDirs += 'build/generated-src/kotlin'
            }
        }
    }
}

kapt {
    correctErrorTypes = true
    useBuildCache = true
    mapDiagnosticLocations = true
}

ext {
    okHttpVersion = '3.10.0'
    retrofitVersion = '2.3.0'
    archVersion = '1.1.1'
    room = '1.0.0'
    supportVersion = '27.1.1'
    glide = '4.6.1'
    koin_version = '0.8.2'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.22.5'

    // support
    implementation "com.android.support:appcompat-v7:${supportVersion}"
    implementation "com.android.support:support-v4:${supportVersion}"
    implementation "com.android.support:recyclerview-v7:${supportVersion}"
    implementation "com.android.support:design:${supportVersion}"
    implementation "com.android.support:cardview-v7:${supportVersion}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'

    // arch
    implementation "android.arch.persistence.room:runtime:${room}"
    kapt "android.arch.persistence.room:compiler:${room}"
    implementation "android.arch.lifecycle:extensions:${archVersion}"
    implementation "android.arch.lifecycle:common-java8:${archVersion}"
    implementation 'android.arch.paging:runtime:1.0.0-rc1'

    // koin
    implementation "org.koin:koin-core:${koin_version}"
    implementation "org.koin:koin-android:${koin_version}"
    implementation "org.koin:koin-android-architecture:${koin_version}"

    // glide
    implementation "com.github.bumptech.glide:glide:${glide}"
    implementation "com.github.bumptech.glide:okhttp3-integration:${glide}"
    kapt "com.github.bumptech.glide:compiler:${glide}"
    implementation 'jp.wasabeef:glide-transformations:3.1.1'
    implementation 'jp.co.cyberagent.android.gpuimage:gpuimage-library:1.4.1'
    implementation 'com.github.florent37:glidepalette:2.1.2'

    // network
    implementation "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-experimental-adapter:1.0.0'
    implementation 'com.github.amalhanaja:retrofit2-kotlin:1.0.0'
    implementation 'jp.wasabeef:recyclerview-animators:2.3.0'

    // view
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'com.github.VladimirWrites:Lemniscate:1.4.4'

    implementation 'com.github.florent37:kotlinpleaseanimate:1.0.1'

    // ads and other
    implementation 'com.yandex.android:mobmetricalib:2.73'
    implementation 'com.yandex.android:mobileads:2.62'

    // test

    //    testImplementation "org.koin:koin-test:${koin_version}"
    testImplementation 'junit:junit:4.12'

    //    testImplementation 'org.robolectric:robolectric:3.0'

    //    testImplementation 'android.arch.core.core-testing:1.1.0'

    //    testImplementation "android.arch.persistence.room:testing:${room}"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation project(':network')
}