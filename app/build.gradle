import java.text.DateFormat
import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

static def getDateTime() {
    DateFormat df = new SimpleDateFormat("dd MMMMM yyyy");
    return df.format(new Date()) + " Ð³."
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 28

    def fileNaming = "apk/WallpapersX"
    def versionNumber = 12
    def versionMajor = "2"
    def versionMinor = "0"
    def versionPatch = "8"

    def versionBuild = -1
    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
        versionProps['VERSION_BUILD'] = versionBuild.toString()
        versionProps['DATE_BUILD'] = getDateTime()
        versionProps.store(versionPropsFile.newWriter(), null)
    }

    def appId = "my.wallpapers.averdsoft"
    defaultConfig {
        applicationId appId
        minSdkVersion 21
        targetSdkVersion 28
        versionCode versionNumber
        versionName "${versionMajor}.${versionMinor}.${versionPatch} (${versionBuild})"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        testInstrumentationRunner "androidx.com.hanks.htextview.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/main.kotlin_module'
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${fileNaming}-${versionMajor}.${versionMinor}.${versionPatch}.apk"
        }
    }
}

ext {
    okHttpVersion = '3.10.0'
    retrofitVersion = '2.3.0'
    archVersion = '2.0.0-rc01'
    room = '2.0.0-rc01'
    supportVersion = '1.0.0-rc02'
    koin_version = '0.9.3'
    coroutines_version = '0.24.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutines_version}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${coroutines_version}"

    // support
    implementation "androidx.appcompat:appcompat:${supportVersion}"
    implementation "androidx.legacy:legacy-support-v4:${supportVersion}"
    implementation "androidx.recyclerview:recyclerview:${supportVersion}"
    implementation 'com.google.android.material:material:1.0.0-rc01'
    implementation "androidx.cardview:cardview:${supportVersion}"
    implementation 'androidx.annotation:annotation:1.0.0-rc02'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'


    implementation "androidx.lifecycle:lifecycle-extensions:${archVersion}"
    implementation "androidx.lifecycle:lifecycle-common-java8:${archVersion}"

    implementation 'androidx.paging:paging-runtime:2.0.0-rc01'

    implementation 'androidx.core:core-ktx:1.0.0-rc02'

    // koin
    def koin_version = "1.0.0-beta-7"
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-scope:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"

    // picasso
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'jp.wasabeef:picasso-transformations:2.2.1'


    // network
    implementation "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    implementation 'ru.gildor.coroutines:kotlin-coroutines-retrofit:0.11.0'

    implementation 'com.mani:ThinDownloadManager:1.4.0'

    // view
    implementation 'com.github.chrisbanes:PhotoView:2.1.4'
    implementation 'net.steamcrafted:load-toast:1.0.12'

    // animation
    implementation 'jp.wasabeef:recyclerview-animators:2.3.0'

    // ads and other
    implementation 'com.yandex.android:mobmetricalib:3.0.0'
    implementation 'com.yandex.android:mobileads:2.71'

    implementation project(':storage')


    //    testImplementation "org.koin:koin-com.hanks.htextview.test:${koin_version}"
    testImplementation 'junit:junit:4.12'

    //    testImplementation 'org.robolectric:robolectric:3.0'

    //    testImplementation 'android.arch.core.core-testing:1.1.0'

    //    testImplementation "android.arch.persistence.room:testing:${room}"
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'
}